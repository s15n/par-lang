WHITESPACE = _{ " " | "\t" | "\n" | "\r" }
COMMENT = _{ "//" ~ (!"\n" ~ ANY)* }

name   = @{ (ASCII_ALPHA ~ ("_" | ASCII_ALPHANUMERIC)*) }
string = @{ "\"" ~ (!("\"" | "\n" | "\r") ~ ANY)* ~ "\"" }

program    = { SOI ~ definition* ~ EOI }
definition = { "define" ~ name ~ "=" ~ expression }

action  = { send | receive | select | close }
send    = { "(" ~ expression ~ ")" }
receive = { "[" ~ name ~ "]"}
select  = { "." ~ name }
close   = { "[]" }

expression = { string | expr_fork | reference }
expr_fork  = { "chan" ~ name ~ "{" ~ process ~ "}" }

reference = { name ~ actions? }

process  = { proc_let | proc_link | command }
proc_let = { "let" ~ name ~ "=" ~ expression ~ ";" ~ process }

command       = { name ~ proc_apply }
proc_apply    = { proc_link | proc_break | proc_continue | proc_case | proc_pass | proc_action }
proc_link     = { "<>" ~ expression }
proc_break    = { "()" }
proc_continue = { "[]" ~ ";" ~ process }
proc_case     = { "{" ~ branches ~ "}" ~ (";" ~ process)? }
proc_pass     = { "pass" }
proc_action   = { action ~ (proc_apply | (";" ~ process)) }

branches    = { branch* }
branch      = { branch_head ~ "=>" ~ "{" ~ process ~ "}" }
branch_head = { name ~ actions? }

actions = { action ~ actions? }
