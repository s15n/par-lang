WHITESPACE = _{ " " | "\t" | "\n" | "\r" }
COMMENT = _{ "//" ~ (!"\n" ~ ANY)* }

program = { SOI ~ definition* ~ EOI }

name = @{ (("_" | ASCII_ALPHA) ~ ("_" | ASCII_ALPHANUMERIC)*) }

definition = { "define" ~ name ~ "=" ~ expression }

expression = { fork | reference | string }
reference  = { name }
fork       = { object ~ "{" ~ process ~ "}" }
string     = @{ "\"" ~ (!("\"" | "\n" | "\r") ~ ANY)* ~ "\"" }

process = { p_let | p_link | p_break | p_continue | p_send | p_receive | p_case | p_select | sugar_oneof }

p_let      = { "let" ~ name ~ "=" ~ expression ~ ";" ~ process }
p_link     = { subject ~ "<>" ~ expression }
p_break    = { subject ~ "(" ~ ")" }
p_continue = { subject ~ "[" ~ "]" ~ ";" ~ process }
p_send     = { subject ~ "(" ~ expression ~ ")" ~ ";" ~ process }
p_receive  = { subject ~ "[" ~ name ~ "]" ~ ";" ~ process }
p_select   = { subject ~ "." ~ branch ~ ";" ~ process }
p_case     = { subject ~ "." ~ "case" ~ "{" ~ p_branches ~ "}" ~ (";" ~ process)? }
p_branches = { p_branch* }
p_branch   = { branch ~ "=>" ~ "{" ~ process ~ "}" }

object    = { name }
subject   = { name }
branch    = { name }

// syntax sugar
sugar_oneof = { subject ~ "." ~ "oneof" ~ "(" ~ sugar_names ~ ")" ~ ";" ~ process }
sugar_names = { name* }
